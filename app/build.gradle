plugins {
    id 'java'
    id "io.freefair.lombok" version "8.13"
    id "run.halo.plugin.devtools" version "0.6.2"
}

group = rootProject.group
version = rootProject.version

jar {
    archiveBaseName.set("aimodel-hub")
    dependsOn(":api:jar")
}

dependencies {
    implementation project(':api')
    implementation platform('run.halo.tools.platform:plugin:2.22.5')
    compileOnly 'run.halo.app:api'

    testImplementation 'run.halo.app:api'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
    
    // 显示测试输出
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
        exceptionFormat = "full"
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release = 21
}

tasks.register('processUiResources', Copy) {
    from project(':ui').layout.buildDirectory.dir('dist')
    into layout.buildDirectory.dir('resources/main/console')
    dependsOn project(':ui').tasks.named('assemble')
    shouldRunAfter tasks.named('processResources')
}

tasks.named('classes') {
    dependsOn tasks.named('processUiResources')
}

halo {
    containerName = 'AiModelHub'
    version = '2.22.10'
}
haloPlugin {
    openApi {
        outputDir = file("$rootDir/api-docs/openapi/v3_0")
        groupingRules {
            aiModelHubApis {
                displayName = 'AI Model Hub Console API'
                pathsToMatch = [
                        '/apis/console.api.aimodel-hub.xhhao.com/v1alpha1/**',
                        '/apis/aimodel-hub.xhhao.com/v1alpha1/**'
                ]
            }
        }
        groupedApiMappings = [
                '/v3/api-docs/aiModelHubApis': 'aiModelHubApis.json'
        ]
        generator {
            outputDir = file("$rootDir/ui/src/api/generated")
        }
    }
}